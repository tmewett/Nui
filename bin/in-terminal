#!/usr/bin/env fish

argparse -s "h/help" -- $argv
or exit 1

function print_help
    echo -n \
"Usage: in-terminal [COMMAND...]
       t [COMMAND...]

Open a new terminal window, optionally with a given command.
"
# TODO Document variables
end

if set -q _flag_help
    print_help
    exit
end

set argc (count $argv)
if set -q _NUI_MACOS
    if test $argc -eq 0
        exec open -a Terminal .
    else
        set script (mktemp -t in-terminal)
        begin
            echo "#!/usr/bin/env fish"
            echo "rm (status filename)"
            echo "cd "(string escape -- (pwd))
            echo (string join -- ' ' (string escape -- $argv))
        end > $script
        chmod +x $script
        exec open -a Terminal $script
    end
end

if test $argc -eq 0
    $NUI_TERMINAL
else
    $NUI_TERMINAL_CMD $argv
end
